val zero :: Parser a
let zero = mkParser (\inp -> [])

val item :: Parser Char
let item = mkParser (\inp -> if (nullStr inp) then []
                             else (headStr inp, tailStr inp) : [])
 
val sat :: (Char -> Bool) -> Parser Char
let sat p = item >>= (\x -> if p x then pure x else zero)
  
val char :: Char -> Parser Char
let char x = sat (\y -> x == y)
    
val mplus :: Parser a -> Parser a -> Parser a
let mplus p q = mkParser(\inp -> let ret = runParser p inp in
                                 if null ret then runParser q inp    
                                 else ret)

val delay :: (Unit -> Parser a) -> Parser a
let delay p = mkParser (\inp -> runParser(p())(inp))

val many :: Parser a -> Parser (List a) 
let many p =  
   let q = pure (:) <*> p <*> delay (\v -> many p) in
   mplus q (pure [])  
   
val sepBy :: Parser a -> Parser b -> Parser (List a)
let sepBy p sep = 
          mplus 
          (p >>= (\x ->
          (many (sep >>= (\s ->
           p >>= pure))) >>= (\xs -> pure (x:xs)))) (pure [])
  
val string :: String -> Parser String  
let string s =  
	let string' cs =  
      if nullStr cs then pure ""
      else pure (\x y -> charToStr x <> y) <*> char (headStr cs) <*> string' (tailStr cs)  
    in string' s 

let foldStr xs = foldr1 (<>) (fmap charToStr xs)

val quotedString :: Parser String     
let quotedString = (char '"') >>= (\x ->
                   (many (sat (\x -> x /= '"'))) >>= (\s ->
                   (char '"') >>= (\y -> (pure . foldStr) s)))

val jsonParser :: Parser Json
let jsonParser =
  let jsonValue =  
     quotedString >>= (\k ->  
     (char ':') >>= (\c ->
     (mplus (fmap JsonValue quotedString) jsonParser) >>= (\v -> pure (k, v)))) in             
  (string "{") >>= (\x ->
  (sepBy jsonValue (string ", ")) >>= (\xs ->
  (string "}") >>= (\x -> pure (JsonNode xs)))) 
 
val parse :: String -> Json
let parse = fst . head . runParser jsonParser 
 
val main :: Json     
let main = parse "{\"firstName\":\"John\", \"lastName\":\"Doe\", \"Child\":{\"firstName\":\"Bart\"}}"

                
