val zero :: Parser a
let zero = mkParser (\inp -> [])

val item :: Parser Char
let item = mkParser (\inp -> if (null inp) then []
                             else (head inp, tail inp) : [])
 
val sat :: (Char -> Bool) -> Parser Char
let sat p = item >>= (\x -> if p x then pure x else zero)
  
val char :: Char -> Parser Char
let char x = sat (\y -> x == y)
    
val mplus :: Parser a -> Parser a -> Parser a
let mplus p q = mkParser(\inp -> let ret = runParser p inp in
                                 if null ret then runParser q inp   
                                 else ret)
 
val many :: Parser a -> Parser (List a)
let many p = mplus (p       >>= (\x ->
                   (many p) >>= (\xs ->
                   pure (x:xs)))) (pure [])
 
val many1 :: Parser a -> Parser (List a)
let many1 p = p >>= (\x ->
              (many p) >>= (\xs -> 
              pure (x:xs))) 
  
val sepBy :: Parser a -> Parser b -> Parser (List a)
let sepBy p sep = 
          mplus 
          (p >>= (\x ->
          (many (sep >>= (\s ->
           p >>= pure))) >>= (\xs -> pure (x:xs)))) (pure [])

val string :: String -> Parser (List Char)
let string s = 
	let string' cs =  
      if null cs then pure []
      else let c   = head cs in
           let cs' = tail cs in
           (char c) >>= (\x1 ->
           (string' cs') >>= (\x2 -> 
           pure (c:cs')))
    in string' (toCharList s) 

val quotedString :: Parser (List Char)     
let quotedString = (char '"') >>= (\x ->
                   (many (sat (\x -> x /= '"'))) >>= (\s ->
                   (char '"') >>= (\y -> pure s)))

val jsonParser :: Parser Json
let jsonParser =
  let jsonValue =  
     quotedString >>= (\k ->  
     (char ':') >>= (\c ->
     (mplus (fmap JsonValue quotedString) jsonParser) >>= (\v -> pure (k, v)))) in             
  (string "{") >>= (\x ->
  (sepBy jsonValue (string ", ")) >>= (\xs ->
  (string "}") >>= (\x -> pure (JsonNode xs)))) 
 
val parse :: String -> Json
let parse = fst . head . runParser jsonParser . toCharList
 
val main :: Json     
let main = parse "{\"firstName\":\"John\", \"lastName\":\"Doe\", \"Child\":{\"firstName\":\"Bart\"}}"

                
